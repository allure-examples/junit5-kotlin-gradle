plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.23'
}

wrapper {
    gradleVersion = '8.7'
}

repositories {
    mavenCentral()
}

group = 'com.example.junit5'
version = '1.0-SNAPSHOT'

def allureVersion = '2.26.0'
def aspectJVersion = '1.9.22'
def kotlinVersion = '1.9.23'

kotlin {
    jvmToolchain(21)
}

configurations {
    agent {
        canBeResolved = true
        canBeConsumed = true
    }
}

dependencies {
    agent "org.aspectj:aspectjweaver:$aspectJVersion"

    testImplementation platform("org.jetbrains.kotlin:kotlin-bom:$kotlinVersion")
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit5"

    testImplementation platform("io.qameta.allure:allure-bom:$allureVersion")
    testImplementation "io.qameta.allure:allure-junit5"

    testImplementation platform("org.junit:junit-bom:5.10.2")
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"

    testImplementation "org.slf4j:slf4j-simple:2.0.12"
}

test {
    ignoreFailures = true
    useJUnitPlatform()
    jvmArgs = [ "-javaagent:${configurations.agent.singleFile}" ]
}
